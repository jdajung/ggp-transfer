;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; Connect Four
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; ROLE Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(role red)
(role black)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; BASE & INPUT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (base (cell ?x ?y ?p)) (x ?x) (y ?y) (role ?p))
(base (control red))
(base (control black))

(<= (base (step ?n))
	(succ ?nm1 ?n))

(<= (input ?p (drop ?x)) (role ?p) (x ?x))
(<= (input ?p noop) (role ?p))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; INIT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(init (control red))
(init (step 1))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; LEGAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (legal red noop)
    (true (control black)))
(<= (legal red (drop ?x))
    (true (control red))
    (columnOpen ?x))
    
(<= (legal black noop)
    (true (control red)))
(<= (legal black (drop ?x))
    (true (control black))
    (columnOpen ?x))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; NEXT Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (next (cell ?x 1 ?player))
    (does ?player (drop ?x))
	  (columnEmpty ?x))
(<= (next (cell ?x ?y2 ?player))
    (does ?player (drop ?x))
    (cellOpen ?x ?y2)
    (succ ?y1 ?y2)
    (not (cellOpen ?x ?y1))) 
(<= (next (cell ?x ?y ?player))
    (true (cell ?x ?y ?player)))
    
(<= (next (control red))
    (true (control black)))
(<= (next (control black))
    (true (control red)))
    
; MOVE COUNT
(<= (next (step ?y))
    (true (step ?x))
    (succ ?x ?y))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; TERMINAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= terminal
    (line red))
(<= terminal
    (line black))
(<= terminal
    (not boardOpen))
(<= terminal
    (true (step 37)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; GOAL Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (goal red 100)
    (line red))
(<= (goal red 50)
    (not (line red))
    (not (line black))
    (not boardOpen))
;(<= (goal red 50)
;    (not (line red))
;    (not (line black))
;    (true (step 37)))
(<= (goal red 0)
    (line black))
(<= (goal red 50)
    (not (line red))
    (not (line black))
    boardOpen)
    
(<= (goal black 100)
    (line black))
(<= (goal black 50)
    (not (line red))
    (not (line black))
    (not boardOpen))
;(<= (goal black 50)
;    (not (line red))
;    (not (line black))
;    (true (step 37)))
(<= (goal black 0)
    (line red))
(<= (goal black 50)
    (not (line red))
    (not (line black))
    boardOpen)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; View Definitions
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(<= (cellOpen ?x ?y)
    (x ?x)
    (y ?y)
    (not (true (cell ?x ?y red)))
    (not (true (cell ?x ?y black))))
(<= (columnOpen ?x)
    (cellOpen ?x 6))
(<= (columnEmpty ?x)
    (cellOpen ?x 1))
(<= boardOpen
    (x ?x)
    (columnOpen ?x))

(<= (line ?player)
    (true (cell ?x1 ?y ?player))
    (succ ?x1 ?x2)
    (succ ?x2 ?x3)
    (succ ?x3 ?x4)
    (true (cell ?x2 ?y ?player))
    (true (cell ?x3 ?y ?player))
    (true (cell ?x4 ?y ?player)))
(<= (line ?player)
    (true (cell ?x ?y1 ?player))
    (succ ?y1 ?y2)
    (succ ?y2 ?y3)
    (succ ?y3 ?y4)
    (true (cell ?x ?y2 ?player))
    (true (cell ?x ?y3 ?player))
    (true (cell ?x ?y4 ?player)))
(<= (line ?player)
    (true (cell ?x1 ?y1 ?player))
    (succ ?x1 ?x2)
    (succ ?x2 ?x3)
    (succ ?x3 ?x4)
    (succ ?y1 ?y2)
    (succ ?y2 ?y3)
    (succ ?y3 ?y4)
    (true (cell ?x2 ?y2 ?player))
    (true (cell ?x3 ?y3 ?player))
    (true (cell ?x4 ?y4 ?player)))
(<= (line ?player)
    (true (cell ?x1 ?y4 ?player))
    (succ ?x1 ?x2)
    (succ ?x2 ?x3)
    (succ ?x3 ?x4)
    (succ ?y3 ?y4)
    (succ ?y2 ?y3)
    (succ ?y1 ?y2)
    (true (cell ?x2 ?y3 ?player))
    (true (cell ?x3 ?y2 ?player))
    (true (cell ?x4 ?y1 ?player)))
    
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Static Relations
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(succ 1 2) (succ 2 3) (succ 3 4) (succ 4 5) (succ 5 6) (succ 6 7) (succ 7 8) (succ 8 9) (succ 9 10) (succ 10 11)
(succ 11 12) (succ 12 13) (succ 13 14) (succ 14 15) (succ 15 16) (succ 16 17) (succ 17 18) (succ 18 19) (succ 19 20) (succ 20 21)
(succ 21 22) (succ 22 23) (succ 23 24) (succ 24 25) (succ 25 26) (succ 26 27) (succ 27 28) (succ 28 29) (succ 29 30) (succ 30 31)
(succ 31 32) (succ 32 33) (succ 33 34) (succ 34 35) (succ 35 36) (succ 36 37)


(x 1) (x 2) (x 3) (x 4) (x 5) (x 6) (x 7) (x 8)
(y 1) (y 2) (y 3) (y 4) (y 5) (y 6)